# Detect the python executable
python := if os_family() == "windows" { "python" } else { "python3" }

# Default command when no command is provided
_default:
    @just --list

# Build all the projects
build:
    @if [ "${OS:-}" = "Windows_NT" ]; then \
        pwsh -File ../../.dev/build.ps1; \
    else \
        bash ../../.dev/build.sh; \
    fi

# Start all the projects
start:
    @if [ "${OS:-}" = "Windows_NT" ]; then \
        pwsh -File ../../.dev/start.ps1; \
    else \
        bash ../../.dev/start.sh; \
    fi

# Initialize Azure environment setting
init:
    azd init

# First Azure provisioning
provision:
    azd provision

# Build all and deploy to Azure (requires first time provisioning)
deploy: build _deploy

# Skip build and deploy to Azure what is already built
deploy-current-build: _deploy

# Build deployment manifest
manifest:
    @{{python}} ../../.dev/aspire-manifest.py

###################
#### Internals ####
###################

# Deploy to Azure (using existing build)
_deploy:
    @{{python}} ../../.dev/aspire-deploy.py
